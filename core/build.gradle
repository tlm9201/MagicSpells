plugins {
    id('com.gradleup.shadow') version '8.3.2'
    id("antlr")
}

dependencies {
    shadow(group: "org.apache.commons", name: "commons-math4-core", version: "4.0-beta1")
    shadow(group: "com.github.Chronoken", name: "EffectLib", version: "e809407")
    shadow(group: "co.aikar", name: "acf-paper", version: "0.5.1-SNAPSHOT")
    shadow(group: "org.bstats", name: "bstats-bukkit", version: "3.0.2")

    shadow(group: "org.antlr", name: "antlr4-runtime", version: "4.13.2")
    antlr(group: "org.antlr", name: "antlr4", version: "4.13.2")

    shadow(project(path: ":nms:shared", configuration: "apiElements"))
    shadow(project(path: ":nms:latest")) { transitive = false }

    implementation(group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.3.0") { transitive = false }
    implementation(group: "com.github.libraryaddict", name: "LibsDisguises", version: "v10.0.25") { transitive = false }
    implementation(group: "net.milkbowl.vault", name: "VaultAPI", version: "1.7") { transitive = false }
    implementation(group: "me.clip", name: "placeholderapi", version: "2.11.6") { transitive = false }
    implementation(group: "com.github.GriefPrevention", name: "GriefPrevention", version: "17.0.0") { transitive = false }
    implementation(group: "com.github.Xezard", name: "XGlow", version: "1.1.0") {
        exclude(module: "XGlowPlugin")
        exclude(module: "XGlowExample")
    }
    implementation(group: "com.sk89q.worldguard", name: "worldguard-core", version: "7.1.0-SNAPSHOT") { transitive = false }
    implementation(group: "com.sk89q.worldguard", name: "worldguard-bukkit", version: "7.1.0-SNAPSHOT") { transitive = false }
    implementation(group: "com.sk89q.worldedit", name: "worldedit-core", version: "7.4.0-SNAPSHOT") { transitive = false }
    implementation(group: "com.sk89q.worldedit", name: "worldedit-bukkit", version: "7.4.0-SNAPSHOT") { transitive = false }

}

base {
    archivesName = "MagicSpells"
}

jar {
    manifest {
        attributes(
            "paperweight-mappings-namespace": "mojang"
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate("org.apache.commons.math4", "com.nisovin.magicspells.shaded.org.apache.commons.math4")
    relocate("de.slikey.effectlib", "com.nisovin.magicspells.shaded.effectlib")
    relocate("co.aikar.commands", "com.nisovin.magicspells.shaded.acf")
    relocate("co.aikar.locales", "com.nisovin.magicspells.shaded.locales")
    relocate("org.bstats", "com.nisovin.magicspells.shaded.bstats")

    // Included by apache/commons-math, copied by this project.
    exclude("META-INF/LICENSE")
    exclude("META-INF/NOTICE")
    // Include our own copies of attributions.
    from("$rootDir/3rd_party_licenses") {
        into "META-INF/3rd_party_licenses"
    }
    from("$rootDir/NOTICE.md") {into "META-INF"}
    from("$rootDir/LICENSE") {into "META-INF"}

    archiveClassifier.set("")
}

sourceSets {
    main {
        java {
            srcDir file("generated-src/antlr/main/")
        }
    }
}

generateGrammarSource {
    arguments += ["-package", "com.nisovin.magicspells.util.grammars", "-visitor", "-no-listener"]
}

artifacts {
    archives shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
