import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id("io.github.goooler.shadow") version "8.1.7"
}

dependencies {
    shadow(group: "org.apache.commons", name: "commons-math4-core", version: "4.0-beta1")
    shadow(group: "com.github.Chronoken", name: "EffectLib", version: "a077573")
    shadow(group: "co.aikar", name: "acf-paper", version: "0.5.1-SNAPSHOT")
    shadow(group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.9.23")
    shadow(group: "org.bstats", name: "bstats-bukkit", version: "3.0.2")

    shadow(project(path: ":nms:shared", configuration: "apiElements"))
    shadow(project(path: ":nms:v1_20_6")) { transitive = false }

    implementation(group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0") { transitive = false }
    implementation(group: "com.github.libraryaddict", name: "LibsDisguises", version: "v10.0.25") { transitive = false }
    implementation(group: "com.github.MilkBowl", name: "VaultAPI", version: "master-SNAPSHOT") { transitive = false }
    implementation(group: "com.github.PlaceholderAPI", name: "PlaceholderAPI", version: "master-SNAPSHOT") { transitive = false }
    implementation(group: "com.github.TechFortress", name: "GriefPrevention", version: "master-SNAPSHOT") { transitive = false }
    implementation(group: "com.github.Xezard", name: "XGlow", version: "master-SNAPSHOT") {
        exclude(module: "XGlowPlugin")
        exclude(module: "XGlowExample")
    }

    implementation(group: "com.github.EngineHub", name: "WorldGuard", version: "master") {
        exclude(module: "worldguard-libs")
    }

    implementation(group: "com.github.EngineHub", name: "WorldEdit", version: "master") {
        transitive = false
        exclude(module: "ap")
        exclude(module: "cli")
        exclude(module: "fabric")
        exclude(module: "forge")
        exclude(module: "sponge")
        exclude(module: "worldedit-cli")
        exclude(module: "worldedit-sponge")
        exclude(module: "worldedit-forge-mc1.16.1")
        exclude(module: "worldedit-fabric-mc1.16.1")
    }

}

base {
    archivesName = "MagicSpells"
}

jar {
    manifest {
        attributes(
            "paperweight-mappings-namespace": "mojang"
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate("org.apache.commons.math4", "com.nisovin.magicspells.shaded.org.apache.commons.math4")
    relocate("de.slikey.effectlib", "com.nisovin.magicspells.shaded.effectlib")
    relocate("kotlin", "com.nisovin.magicspells.shaded.kotlin")
    relocate("co.aikar.commands", "com.nisovin.magicspells.shaded.acf")
    relocate("co.aikar.locales", "com.nisovin.magicspells.shaded.locales")
    relocate("org.bstats", "com.nisovin.magicspells.shaded.bstats")
    archiveClassifier.set("")
}

compileKotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
        javaParameters.set(true)
    }
}

compileTestKotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
    }
}

artifacts {
    archives shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
